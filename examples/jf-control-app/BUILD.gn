# Copyright (c) 2024 Project CHIP Authors
#
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
# http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

import("//build_overrides/build.gni")
import("//build_overrides/chip.gni")
import("//build_overrides/editline.gni")

import("${chip_root}/build/chip/tools.gni")
import("${chip_root}/examples/jf-control-app/jf-control-app.gni")
import("${chip_root}/src/lib/core/core.gni")

assert(chip_build_tools)

config("config") {
  include_dirs = [
    ".",
    "${chip_root}/examples/common",
    "${chip_root}/examples/platform/linux",
    "${chip_root}/zzz_generated/app-common/app-common",
    "${chip_root}/zzz_generated/chip-tool",
    "${chip_root}/src/lib",
  ]

  defines = [ "CONFIG_USE_SEPARATE_EVENTLOOP=${config_use_separate_eventloop}" ]

  if (config_enable_grpc) {
    defines += [ "CONFIG_ENABLE_GRPC=1" ]
  }

  # Note: CONFIG_USE_LOCAL_STORAGE is tested for via #ifdef, not #if.
  if (config_use_local_storage) {
    defines += [ "CONFIG_USE_LOCAL_STORAGE" ]
  }

  cflags = [ "-Wconversion" ]
}

static_library("jf-control-app-utils") {
  sources = [
    "${chip_root}/src/controller/ExamplePersistentStorage.cpp",
    "${chip_root}/src/controller/ExamplePersistentStorage.h",
    "${chip_root}/zzz_generated/chip-tool/zap-generated/cluster/ComplexArgumentParser.cpp",
    "${chip_root}/zzz_generated/chip-tool/zap-generated/cluster/logging/DataModelLogger.cpp",
    "${chip_root}/zzz_generated/chip-tool/zap-generated/cluster/logging/EntryToText.cpp",
    "commands/clusters/ModelCommand.cpp",
    "commands/clusters/ModelCommand.h",
    "commands/common/BDXDiagnosticLogsServerDelegate.cpp",
    "commands/common/CHIPCommand.cpp",
    "commands/common/CHIPCommand.h",
    "commands/common/Command.cpp",
    "commands/common/Command.h",
    "commands/common/Commands.cpp",
    "commands/common/Commands.h",
    "commands/common/CredentialIssuerCommands.h",
    "commands/common/HexConversion.h",
    "commands/common/RemoteDataModelLogger.cpp",
    "commands/common/RemoteDataModelLogger.h",
    "commands/joint-fabric/JointFabricCommand.cpp",
    "commands/joint-fabric/JointFabricCommand.h",
    "commands/icd/ICDCommand.cpp",
    "commands/icd/ICDCommand.h",
    "commands/pairing/OpenCommissioningWindowCommand.cpp",
    "commands/pairing/OpenCommissioningWindowCommand.h",
    "commands/pairing/PairingCommand.cpp",
    "commands/pairing/ToTLVCert.cpp",
    "commands/rpc/RpcCommands.cpp",
    "commands/rpc/RpcCommands.h",
    "commands/rpc/RpcConnect.cpp",
    "commands/rpc/RpcConnect.h",
    "device_manager/DeviceManager.cpp",
    "device_manager/DeviceManager.h",
    "RpcClient.cpp",
    "RpcClient.h",
    "RpcServer.cpp",
    "RpcServer.h",
    "JFAdmin.cpp",
    "JFAdmin.h",
    "${chip_root}/examples/platform/linux/system_rpc_server.cc",
    "${chip_root}/examples/platform/linux/RpcClientProcessor.cpp",
    "${chip_root}/examples/platform/linux/RpcClientProcessor.h"
  ]

  if (config_enable_grpc) {
    sources += [
      "SocketServer.cpp",
      "SocketServer.h"
    ]
  }

  deps = [ "${chip_root}/src/app:events" ]

  sources += [ "commands/interactive/InteractiveCommands.cpp" ]
  deps += [
    "${chip_root}/examples/common/websocket-server",
    "${chip_root}/src/platform/logging:headers",
    "${editline_root}:editline",
  ]

  if (chip_device_platform == "darwin") {
    sources += [ "commands/common/DeviceScanner.cpp" ]
  }

  public_deps = [
    "$dir_pw_hdlc:pw_hdlc",
    "$dir_pw_hdlc:default_addresses",
    "$dir_pw_hdlc:rpc_channel_output",
    "$dir_pw_chrono:system_timer",
    "$dir_pw_stream:socket_stream",
    "$dir_pw_rpc:server",
    "$dir_pw_rpc/system_server:facade",
    "$dir_pw_rpc/system_server:socket",
    "$dir_pw_thread:thread",
    "${chip_root}/examples/common/tracing:commandline",
    "${chip_root}/examples/common/pigweed:joint_fabric.nanopb_rpc",
    "${chip_root}/src/app/icd/client:handler",
    "${chip_root}/src/app/icd/client:manager",
    "${chip_root}/src/app/server",
    "${chip_root}/src/app/tests/suites/commands/interaction_model",
    "${chip_root}/src/controller/data_model",
    "${chip_root}/src/credentials:file_attestation_trust_store",
    "${chip_root}/src/credentials:test_dac_revocation_delegate",
    "${chip_root}/src/lib",
    "${chip_root}/src/lib/core:types",
    "${chip_root}/src/lib/support/jsontlv",
    "${chip_root}/src/platform",
    "${chip_root}/third_party/inipp",
    "${chip_root}/third_party/jsoncpp",
  ]

  if (config_enable_grpc) {
    public_deps += [
      "$dir_pw_grpc:connection",
      "$dir_pw_grpc:pw_rpc_handler",
      "$dir_pw_grpc:send_queue",
      "$dir_pw_thread:test_thread_context",
      "${chip_root}/examples/common/pigweed:rpc_services"
    ]
  }

  public_configs = [ ":config" ]

  if (chip_enable_transport_trace) {
    public_deps +=
        [ "${chip_root}/examples/common/tracing:trace_handlers_decoder" ]
  }

  public_deps += pw_build_LINK_DEPS

  output_dir = root_out_dir
}

executable("jf-control-app") {
  sources = [ "main.cpp" ]

  deps = [
    ":jf-control-app-utils",
    "${chip_root}/src/platform/logging:stdio",
  ]

  output_dir = root_out_dir
}

group("default") {
  deps = [ ":jf-control-app" ]
}
